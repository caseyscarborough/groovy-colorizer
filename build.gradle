plugins {
  id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'

group = 'com.caseyscarborough.colorizer'
version = '1.0.0'

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.3.3'
  testCompile 'junit:junit:4.11'
}

test {
  testLogging {
    events 'started', 'passed'
  }
}

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
  classifier "sources"
}

publishing {
  publications {
    GroovyColorizerPublication(MavenPublication) {
      from components.java
      groupId 'com.caseyscarborough.colorizer'
      artifactId 'groovy-colorizer'
      version '1.0.0'

      artifact sourceJar
    }
  }
}

bintray {
  user = System.getenv("BINTRAY_USER")
  key = System.getenv("BINTRAY_KEY")
  publications = ['GroovyColorizerPublication']

  pkg {
    repo = 'maven'
    name = 'groovy-colorizer'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/caseyscarborough/groovy-colorizer'

    version {
      name = '1.0.0'
      desc = 'A Groovy library for colorizing terminal output.'
      released = new Date()
      vcsTag = '1.0.0'
    }
  }
}